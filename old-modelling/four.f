
C * * * * * * * * S I N T E F  -  G E O P H Y S I C S * * * * * * * * *
C
C * * * * * * * * * * * * S U B R O U T I N E S * * * * * * * * * * * *
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C	SUBROUTINE FOUR IS MODIFICATION OF FORK
C	NAME CHANGED DUE TO NAME CONFLICT WITH UNIX
C	MODIFICATIONS:
C	   CX IS DEFINED WITH LENGTH LXDIM WHICH IS GE ACTUAL LENGTH LX
C	   SIGN OF EXPONENT IS READ AS INTEGER NSIGN
C	   NORMALIZATION CORRESPONDS TO
C		F(OM) = INT[F(T)*EXP(I*OM*T)*DT]
C		F(T) = (1/(2*PI))*INT[F(OM)*EXP(-I*OM*T)*DOM]
C	   CHANGE IN NORMALIZATION IS REASON WHY DT NOW APPEARS IN CALL
C	   CALL OF SUBROUTINE IS NOW:  CALL FOUR(CX,LXDIM,LX,DT,NSIGN)
C	ARNE REITAN  1993
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C TITLE    : FORK
C AUTHOR   : CONVERTED TO ND-500 BY O.F.
C DATE     : 06-JAN-84.
C SYSTEM   : ND-500
C FUNCTION : FAST FOURIER TRANSFORM OF COMPLEX VECTOR
C VERSION  : V00.00
C
C * * * * * * * * * * * U P D A T E  H I S T O R Y * * * * * * * * * * *
C
C DATE       BY  NN-VXX.YY   REASON
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C CALL: CALL FORK(LX,CX,SIGNI)
C
C ARGUMENTS: <NAME>  <I/O>  <TYPE>  <EXPLANATION>
C
C             LX       I     I*4     LENGTH OF VECTOR CX
C             CX      I/O    C*4     I: VECTOR TO BE FOURIERTRANSFORMED
C                                    O: FOURIERTRANSFORMED VECTOR
C            SIGNI     I     R*4     SIGN OF EXPONENT
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C SUBROUTINES CALLED : NONE
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C ERROR MESSAGES : NONE
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C FURTHER COMMENTS: THE ROUTINE CALCULATES THE FUNCTION :
C
C                          J=LX
C      CX(K) = SQRT(1/LX)* SUM( CX(J)*EXP(2*PI*SIGNI*(J-1)*(K-1)/LX) )
C                          J=1
C                                FOR K = 1,2,...,LX
C
C      BE AWARE OF THE SCALING WITH SQRT(1/LX) FOR EACH TRANSFORM.
C
C      REF.: J.F.CLAERBOUT : FUNDAMENTALS OF GEOPHYSICAL DATA
C                            PROCESSING, 1976, MCGRAW-HILL INC.
C
C
C
C  N O T E
C
C
C  1.  LX MUST BE A POWER OF 2
C
C  2.  POSITIVE FREQUENCIES ARE RETURNED IN LOCATIONS 1 (DC COMPONENT)
C      TO LX/2+1 (NYQUIST FREQUENCY), NEGATIVE FREQUENCIES ARE
C      RETURNED IN LOCATIONS LX TO LX/2+2 (FREQUENCY NEXT TO NEGATIVE
C      NYQUIST)
C
C  3.  BE AWARE OF POSSIBLE SIGN ERRORS (PROBABLY DUE TO ND COMPILER
C      ERRORS)
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C      THIS BLOCK CHANGED BY A.R.
C      SUBROUTINE FORK(LX,CX,SIGNI)
       SUBROUTINE FOUR(CX,LXDIM,LX,DT,NSIGN)
C      COMPLEX CX(1),CARG,CEXP,CW,CTEMP
       COMPLEX CX(LXDIM),CARG,CEXP,CW,CTEMP
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C----------------------------------------------------------------------
C      TEST THAT LX IS A POWER OF TWO
C----------------------------------------------------------------------
      INTEGER ITAB(12)
      DATA (ITAB(I),I=1,12) /2,4,8,16,32,64,128,256,512,1024,2048,4096/
      DO 100 I=1,12
         IF(LX.EQ.ITAB(I)) THEN
           GOTO 101
         ENDIF
  100 CONTINUE
      PRINT *,'LX NOT A POWER OF TWO'
      S T O P
  101 CONTINUE
C----------------------------------------------------------------------
C      TEST FINISHED
C----------------------------------------------------------------------
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C      THIS STATEMENT INTRODUCED BY A.R.
       SIGNI=NSIGN
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
       J=1
       SC=SQRT(1.0/REAL(LX))
       DO 30 I=1,LX
         IF(I.GT.J) GO TO 10
         CTEMP=CX(J)*SC
         CX(J)=CX(I)*SC
         CX(I)=CTEMP
10       M=LX/2
20       IF(J.LE.M) GOTO 30
         J=J-M
         M=M/2
         IF(M.GE.1) GOTO 20
30     J=J+M
       L=1
40     ISTEP=2*L
       DO 50 M=1,L
         CARG=(0.,1.)*(3.14159265*SIGNI*(M-1))/L
         CW=CEXP(CARG)
         DO 50 I=M,LX,ISTEP
           CTEMP=CW*CX(I+L)
           CX(I+L)=CX(I)-CTEMP
50     CX(I)=CX(I)+CTEMP
       L=ISTEP
       IF(L.LT.LX) GOTO 40
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C      THIS BLOCK INTRODUCED BY A.R.
C      DUE TO CHANGE IN NORMALIZATION
       FACT=SC/DT
       IF(NSIGN.EQ.1) FACT=1./FACT
       DO 1000 I=1,LX
       CX(I)=FACT*CX(I)
 1000  CONTINUE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
       RETURN
       END





